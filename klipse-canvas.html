<!DOCTYPE html>

<html lang="en">
<head>
  <meta charset="utf-8">
  <title>2D Drawing on a html5 canvas with Klipse</title>

<link rel="stylesheet" type="text/css"  
  href="http://app.klipse.tech/css/codemirror.css">

<script>  
  window.klipse_settings = {
        selector: '.language-klipse',
        selector_js: '.language-klipse-js'
};
</script>

</head>

<body>

  <pre>
    <code class="language-klipse">
(def canvas (js/document.getElementById "canvas-2d"))
(def ctx    (.getContext canvas "2d"))
(def twoPI  (* 2 (.-PI js/Math)))

(defn draw-point [x y]
  (let [r 5] ;; radius
    (set! (.-fillStyle   ctx) "blue")
    (.beginPath ctx)
    (.arc ctx x y r 0 twoPI)
    (.fill ctx)))


(defn draw-bezier-curve [[x1 y1] [x2 y2] [x3 y3]]
  ;; draw curve
  (set! (.-strokeStyle ctx) "red")
  (set! (.-lineWidth   ctx) 2)
  (.beginPath ctx)
  (.moveTo ctx x1 y1)
  (.quadraticCurveTo ctx x2 y2 x3 y3)
  (.stroke ctx)
  (.closePath ctx)
  ;; draw control points
  (draw-point x1 y1)
  (draw-point x2 y2)
  (draw-point x3 y3))


(let [wc (.-width canvas)  ;; width of *html* canvas
      hc (.-height canvas) ;; height of *html* canvas
      ratio 0.9            ;; ratio of *html* canvas to use as *drawing* canvas
      t  (/ (- 1 ratio) 2) ;; translation constant
      w  (* ratio wc)      ;; width of *drawing* canvas 
      h  (* ratio hc)      ;; height of *drawing* canvas
      x  (* 1/2 w)         ;; x val between 0 and w
      y  (* 1/2 h)]        ;; y val between 0 and h

  (.clearRect ctx 0 0 wc hc)  ;; clear html canvas

  (.save ctx)
  (.translate ctx (* t wc) (* t hc))

  ;; draw a grey border around the drawing canvas
  (set! (.-lineWidth ctx) 1)
  (set! (.-strokeStyle ctx) "grey")
  (.strokeRect ctx 0 0 w h)

  ;; (Un)comment what you want to show / hide
  ;; Change the coordinates to get a feel for the bezier curves

  ;; (draw-point 0 0)
  ;; (draw-point x y)
  ;; (draw-point w h)

  ;; Increasing ascending curve
  (draw-bezier-curve [0 h] [w h] [w 0])
  
  ;; Swoosh
  ;; (draw-bezier-curve [0 y] [0 h] [w 0])

  ;; normal curve
  ;; (draw-bezier-curve [0 h] [x 0] [w h])

  ;; my pulse after a useless meeting
  ;; (draw-bezier-curve [0 y] [x y] [w y])

  (.restore ctx))
    </code>
  </pre>
  <canvas id="canvas-2d" width="800" height="800"></canvas>


  <script src="http://app.klipse.tech/plugin/js/klipse_plugin.js?"></script>
</body>
</html>
